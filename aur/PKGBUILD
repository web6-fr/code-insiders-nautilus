# Maintainer: web6 <contact@web6.fr>
pkgname=code-insiders-nautilus-git
pkgver=r9.ddff975
pkgrel=2
pkgdesc="VSCode Insiders extension for Nautilus"
arch=('i686' 'x86_64')
url="https://github.com/web6-fr/code-insiders-nautilus"
license=('custom')
depends=('python-nautilus')
makedepends=('git')
optdepends=('code-insiders: The Visual Studio Code Insiders (vscode-insiders) editor')
install="${pkgname}.install"
source=("${pkgname}::git+${url}.git")
md5sums=('SKIP')
pkgver() {
    cd "$pkgname"
    (
        set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

package() {
    cd "$pkgname"
    install -Dm755 code-nautilus.py "$pkgdir/usr/share/nautilus-python/extensions/code-insiders-nautilus.py"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
